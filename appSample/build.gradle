plugins {
	id 'com.android.application' version '7.2.2'
	id 'com.gladed.androidgitversion' version '0.4.14'
}

androidGitVersion {
	prefix = "v"
	codeFormat = 'MMNNPPBBB'
}

android {
	compileSdkVersion 33

	defaultConfig {
		applicationId "org.connectbot"
		versionName androidGitVersion.name()
		versionCode androidGitVersion.code()

		minSdkVersion 21
		targetSdkVersion 31

		vectorDrawables.useSupportLibrary = true
	}

	signingConfigs {
		if (project.hasProperty('keystorePassword')) {
			release {
				storeFile file(property('keystoreFile'))
				storePassword property('keystorePassword')
				keyAlias property('keystoreAlias')
				keyPassword property('keystorePassword')
			}
		}
	}
	buildTypes {
		release {
			shrinkResources true
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'

			if (project.hasProperty('keystorePassword')) {
				signingConfig signingConfigs.release
			}

			return true // this silences Android Studio's groovy inspector
		}

		debug {
			// This is necessary to avoid using multiDex
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg', 'proguard-debug.cfg'
			minifyEnabled true

			applicationIdSuffix ".debug"
			testCoverageEnabled true
		}
	}

	packagingOptions {
		exclude 'META-INF/LICENSE.txt'
		exclude 'LICENSE.txt'
		exclude '**/*.gwt.xml'
	}
}

dependencies {
	implementation 'org.connectbot:sshlib:2.2.21'
	googleImplementation 'com.google.android.gms:play-services-basement:18.1.0'
	ossImplementation 'org.conscrypt:conscrypt-android:2.5.2'

	implementation "androidx.recyclerview:recyclerview:1.3.0"
	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation "androidx.preference:preference:1.2.0"
	implementation "com.google.android.material:material:1.8.0"
}
